!  ShengBTE, a solver for the Boltzmann Transport Equation for phonons
!  Copyright (C) 2012-2013 Wu Li <wu.li.phys2011@gmail.com>
!  Copyright (C) 2012-2013 Jesús Carrete Montaña <jcarrete@gmail.com>
!  Copyright (C) 2012-2013 Nebil Ayape Katcho <nebil.ayapekatcho@cea.fr>
!  Copyright (C) 2012-2013 Natalio Mingo Bisquert <natalio.mingo@cea.fr>
!
!  This program is free software: you can redistribute it and/or modify
!  it under the terms of the GNU General Public License as published by
!  the Free Software Foundation, either version 3 of the License, or
!  (at your option) any later version.
!
!  This program is distributed in the hope that it will be useful,
!  but WITHOUT ANY WARRANTY; without even the implied warranty of
!  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!  GNU General Public License for more details.
!
!  You should have received a copy of the GNU General Public License
!  along with this program.  If not, see <http://www.gnu.org/licenses/>.

! Code related to the irreducible wedge of the BZ.

module wedgetc
  use symmetry
  use config
  use misc
  implicit none

contains

  ! Find q-points in the irreducible wedge. The Brillouin zone is
  ! discretized into product(ngrid) points through Ngrid(:) divisions
  ! along each lattice vector. List(1:Nlist) gives Nlist points in the
  ! irreducible wedge. Nequi(1:Nlist) gives the number of equivalent
  ! points in the whole Brillouin zone.  These q-points are indexed
  ! along the G1 direction first, then the G2 direction and finally
  ! the G3 direction. In other words, when the single index is
  ! increased, the G1 component changes first, then the G2 component
  ! and finally the G3 component.
  subroutine wedge(Nlist,Nequi,List,ALLEquiList,TypeofSymmetry)
    implicit none

    include "mpif.h"

    integer(kind=4),intent(out) :: Nlist,Nequi(nptk),List(nptk)
    integer(kind=4),intent(out) :: ALLEquiList(Nsymm,nptk),TypeofSymmetry(Nsymm,nptk)

    integer(kind=4) :: ID_equi(nsymm,nptk),ii,jj,ll,iaux,Ntot,ierr

    integer(kind=4) :: NAllList,AllList(nptk),EquiList(nsymm)

    call symmetry_map(ID_equi)

    Ntot=0
    NList=0
    NAllList=0
    List=0
    AllList=0
    do ii=1,nptk
       iaux=1
       if (ii.ne.1) then
          do jj=1,NAllList
             if (ii.eq.AllList(jj)) then
                iaux=0
                exit
             endif
          end do
       end if
       ! If iaux=0, the current q point is equivalent to some points
       ! included in AllList, which contains all the equivalent points
       ! of the previous q points.
       if (iaux.eq.1) then
          Nlist=Nlist+1
          List(Nlist)=ii
          Nequi(Nlist)=0
          
          EquiList(:)=ID_equi(:,ii)
          if(any(EquiList.eq.-1)) then
             if(myid.eq.0)write(error_unit,*) "Error: and incompatible symmetry was not removed"
             call MPI_BARRIER(MPI_COMM_WORLD,ierr)
             call MPI_FINALIZE(ierr)
          end if
          do ll=1,Nsymm
             iaux=1
             if (ll.ne.1) then
                do jj=1,Nequi(Nlist)
                   If(EquiList(ll).eq.EquiList(jj)) then
                      iaux=0
                      exit
                   end if
                end do
             end if
             ! If iaux=0, the point generated by the ll-th symmetric
             ! operation on q either does not belong to the grid or is
             ! equivalent to some points included in EquiList, which
             ! contains all the equivalent points generated by the
             ! previous operations.
             if (iaux.eq.1) then
                Nequi(Nlist)=Nequi(Nlist)+1
                EquiList(Nequi(Nlist))=EquiList(ll)
                NAllList=NAllList+1
                AllList(NAllList)=EquiList(Nequi(Nlist))
                ALLEquiList(Nequi(Nlist),Nlist)=EquiList(Nequi(Nlist))
                TypeofSymmetry(Nequi(Nlist),Nlist)=ll
             end if
          end do
          Ntot=Ntot+Nequi(Nlist)
       end if
    end do

    ! Print a summary of the findings.
    if(myid.eq.0)write(*,*) "Info: Ntot =",Ntot
    if(myid.eq.0)write(*,*) "Info: Nlist =",Nlist
    if(ntot.ne.nptk) then
       if(myid.eq.0)write(error_unit,*) "Error: ntot!=nptk"
       call MPI_BARRIER(MPI_COMM_WORLD,ierr)
       call MPI_FINALIZE(ierr)
    end if
  end subroutine wedge
end module wedgetc
